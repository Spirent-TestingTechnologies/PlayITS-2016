/**
 * @shortdesc Doors 
 * @desc Testcase that tests opening the door
 * @relexecpath clf/Testsuite.clf
 */
module Doors {
	import from PhyIOAUX all;
	
	import from Components all;
	import from Functions all;
	import from Parameters all;

	/**
	 * @shortdesc Open Door
	 * @desc In this testcase the door should be opened once.
	 * @verdict passes if the door is opened within the given time window otherwise it fails.
	 * @stage 1:Open door
	 */
	testcase OpenDoorTestcase() runs on me system System {
		var default alts := activate(alt_default());
		
		setup();
    	
		t.start(TIME_DOORS);
		doorSensor.send(ReadDoorStart:{});
		
		setStage(1, TIME_DOORS);
    	
		alt{
			[]doorSensor.receive(DoorState: true) {
				setverdict(pass, "Door was opened");
			}
			[]doorSensor.receive(DoorState: false) {
				// false should never be sent but if it is we don't bother.
				log("Door is closed -> repeat");
				repeat;
			}
			[]t.timeout {
				setverdict(fail, "Sensor sent no data");
			}
		}

		doorSensor.send(ReadDoorStop:{});

		tearDown();
	}
	
	function executeDoorTestcases() {
		execute(OpenDoorTestcase());
	}
}