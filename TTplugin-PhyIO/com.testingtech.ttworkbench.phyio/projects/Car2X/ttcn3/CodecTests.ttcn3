/**
 * @desc Unit tests for the codec
 */
module CodecTests language "TTCN-3:2010 Advanced Parameterization" {
	import from PhyIOAUX all;
	
	import from Components all;
	
	type record of boolean RecordList;
	
	type anytype TT with {encode "PhyIO"};
	
	type record CodecPair {TT v, charstring s, charstring d}
	
	type record of CodecPair CodecPairList;
	
	const CodecPairList ENCODE_LIST := {
		// LED
		{{SwitchLED := true},				"30, 1, 1",					"LED on"},
		{{SwitchLED := false},				"30, 1, 0",					"LED off"},
		{{BlinkLEDStart := 0.4},			"30, 2, 0.4",				"LED blink start"},
		{{BlinkLEDStop := {}},				"30, 3",					"LED blink stop"},
		
		// RFID
		{{ReadRFIDStart := {}},				"80, 2",					"RFID start"},
		{{ReadRFIDStop := {}},				"80, 3",					"RFID stop"},
		{{RFID := {255, 0, 2, 156}},		"80, 1, 255, 0, 2, 156",	"RFID setup"},
		
		// Door
		{{ReadDoorStart := {}},				"60, 2",					"Door start"},
		{{ReadDoorStop := {}},				"60, 3",					"Door stop"},
		
		// Distance Sensor
		{{DistanceSensorSetup := 0.1},		"21, 1, 0.1",				"Distance setup"},
		{{ReadDistanceStart := {}},			"21, 2",					"Distance start"},
		{{ReadDistanceStop := {}},			"21, 3",					"Distance stop"},
		
		// Light Sensor
		{{ReadFrequencyStart := 8},			"70, 2, 8",					"Frequency start"},
		{{ReadFrequencyStop := {}},			"70, 3",					"Frequency stop"},
		{{ReadLightSensorState := false},	"70, 4, 0",					"Read light sensor"},
		{{ReadLightSensorState := true},	"70, 4, 1",					"Read frequency"},
		
		// Theft Detection
		{{TheftStart := {255, 0, 2, 156}},	"85, 2, 255, 0, 2, 156",	"Start theft detection"},
		{{TheftStop := {}},					"85, 3",					"Stop theft detection"},
		
		// things we don't need but test anyways to ensure backward compatibility
		// these only include things that were implemented in the beginning
		{{ReadRGB := {}},					"10, 4",					"Read RGB"},
		{{RelaySwith := true},				"40, 1, 1",					"Switch relay on"},
		{{RelaySwith := false},				"40, 1, 0",					"Switch relay off"},
		{{ReadDistance := {}},				"20, 4",					"Read Ping Echo"},
		{{Velocity := -34},					"50, 1, -34",				"Set motor negative"},
		{{Velocity := 68},					"50, 1, 68",				"Set motor positive"}
	};
	
	testcase EncodingTestcase() runs on me system me {
		var RecordList verdicts;
		
		for(var integer i := 0; i < lengthof(ENCODE_LIST); i := i  + 1) {
			if(checkEncode(ENCODE_LIST[i])) {
				log("Success: " & ENCODE_LIST[i].d);
			} else {
				setverdict(fail, "Fail at:", ENCODE_LIST[i].d);
			}
		}
		
		// sadly impossible to put these into a list
		if(not checkDecode<RFIDState>(RFIDState: {1230, {255, 0, 2, 156}},		"80, 101, 1230, 255, 0, 2, 156")) {
			setverdict(fail, "Fail at: RFID answer");
		} else {
			log("Success: RFID answer");
		}
		
		if(not checkDecode<DoorState>(DoorState: true,							"60, 101, 1")) {
			setverdict(fail, "Fail at: Door answer");
		} else {
			log("Success: Door answer");
		}
		
		if(not checkDecode<DistanceSensorState>(DistanceSensorState: {1002, 3},	"21, 101, 1002, 3")) {
			setverdict(fail, "Fail at: Distance answer");
		} else {
			log("Success: Distance answer");
		}
				
		if(not checkDecode<State>(State: {37, 1002},							"70, 101, 37, 1002")) {
			setverdict(fail, "Fail at: Brightness answer");
		} else {
			log("Success: Brightness answer");
		}
				
		if(not checkDecode<Frequency>(Frequency: 3.25,							"70, 101, 3.25")) {
			setverdict(fail, "Fail at: Frequency answer");
		} else {
			log("Success: Frequency answer");
		}
		
		// things we don't need but test anyways to ensure backward compatibility
		if(not checkDecode<RGB>(RGB: {0, 128, 255},								"10, 101, 0, 128, 255")) {
			setverdict(fail, "Fail at: RGB answer");
		} else {
			log("Success: RGB answer");
		}
		
		if(not checkDecode<Distance>(Distance: 20.2,							"20, 101, 20.2")) {
			setverdict(fail, "Fail at: Ping Echo answer");
		} else {
			log("Success: Ping Echo answer");
		}

		if(getverdict == none) {
			setverdict(pass);
		}
	}
	
	function checkEncode(CodecPair p) return boolean {
		var bitstring b := encvalue(p.v);
	
		var charstring str := oct2char(bit2oct(b));
		
		return str == p.s;
	}
	
	function checkDecode<T>(T v, charstring s) return boolean {
		var T expected := v;
		
		var bitstring bt := oct2bit(char2oct(s));
		var integer r := decvalue(bt, v);
		
		return r == 0 and expected == v;
	}
}