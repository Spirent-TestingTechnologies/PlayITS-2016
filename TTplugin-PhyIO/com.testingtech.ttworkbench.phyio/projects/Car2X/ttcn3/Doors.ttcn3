//** @desc Testing Door Status
module Doors {
	import from PhyIOAUX all;
	
	import from Components all;
	import from Functions all;
	import from Parameters all;

	/**
	 * @shortdesc Open Door
	 * @desc In this testcase the door should be opened once.
	 * @verdict passes if the door is opened within the given time window otherwise it fails.
	 */
	testcase OpenDoorTestcase() runs on me system System {
		setup();
    	
		t.start(TIME_DOORS);
		doorSensor.send(ReadDoorStart:{});
    	
		alt{
			[]doorSensor.receive(DoorState: true) {
				log("Door was opened");
				setverdict(pass);
			}
			[]doorSensor.receive(DoorState: false) {
				// false should never be sent but if it is we don't bother.
				log("Door is closed");
				repeat;
			}
			[]t.timeout {
				log("Sensor sent no data.");
				setverdict(fail);
			}
			[]any port.receive {
				log("Unexpected Message");
				setverdict(fail);
			}
		}

		doorSensor.send(ReadDoorStop:{});

		tearDown();
	}
	
	function executeDoorTestcases() {
		execute(OpenDoorTestcase());
	}
}