/*
 * ----------------------------------------------------------------------------
 *  (C) Copyright Testing Technologies 2015-2016.  All Rights Reserved.
 *
 *  All copies of this program, whether in whole or in part, and whether
 *  modified or not, must display this and all other embedded copyright
 *  and ownership notices in full.
 *
 *  See the file COPYRIGHT for details of redistribution and use.
 *
 *  You should have received a copy of the COPYRIGHT file along with
 *  this file; if not, write to the Testing Technologies,
 *  Michaelkirchstrasse 17/18 10179 Berlin, Germany
 *
 *  TESTING TECHNOLOGIES DISCLAIMS ALL WARRANTIES WITH REGARD TO THIS
 *  SOFTWARE. IN NO EVENT SHALL TESTING TECHNOLOGIES BE LIABLE FOR ANY
 *  SPECIAL, DIRECT, INDIRECT OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
 *  WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN
 *  AN ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION,
 *  ARISING OUT OF OR IN CONNECTION WITH THE USE OR PERFORMANCE OF
 *  THIS SOFTWARE.
 *
 *  THE SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND,
 *  EITHER EXPRESSED OR IMPLIED, INCLUDING ANY KIND OF IMPLIED OR
 *  EXPRESSED WARRANTY OF NON-INFRINGEMENT OR THE IMPLIED WARRANTIES
 *  OF MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE.
 * ----------------------------------------------------------------------------
 */
module PhyIOExample {

    import from TTlibrary all;
	import from RS232ConfigPortAux all;

	import from PhyIOAUX all;
	modulepar float TIMEOUT := 10.0;
	
	const integer GREEN := 0;
	const integer BLUE := 1;
	const integer A := 0;
	const integer B := 1;
	
	const UpdateFrequency LED_REFRESH_RATE := 1.0; // seconds
	const UpdateFrequency RGB_REFRESH_RATE := 1.0; // seconds
	
	modulepar RS232Config px_rs232Configuration := {
		serialPortName := "COM3",
		baudRate := c_baudRate_115200,
		dataBits := 8,
		stopBits := 1,
		parity := c_ParityNone,
		flowControl := c_FlowControlNone,
		checkDSR := false
	};

	type component Configuration {
		port PhyConfig config;
	}
	type component me extends Configuration {
		port PhyLED leds[2];
		port PhyRelay relays[2];
		port PhyColorSensor rgbSensor;
	}
	type component Evaluation extends Configuration {
		port PhyEcho echoSensor;
		port PhyMotor motor;
		port PhyColorSensor rgbSensor;
	}
	type component PhyIO extends Evaluation {
		port PhyLED leds[2];
		port PhyRelay relays[2];
	}
	testcase PingTest() runs on Evaluation system PhyIO {
		timer t;

		map(self:config, system:config) param (px_rs232Configuration, DeviceID_1);
		map(self:echoSensor, system:echoSensor) param (DeviceID_1, SensorID_8, LED_REFRESH_RATE);
		
		var default cfgUpdates := activate(configUpdates());

		var Distance d := f_ReadDistance();
		if (d > 0.0) {
			setverdict(pass, sprintf1<float>("Received distance %f cm", d));
		} else {
			setverdict(fail, "No answer from distance sensor");
		}
	}

	testcase SetupTest1() runs on me system PhyIO {
		timer t;

		map(self:config, system:config) param (px_rs232Configuration, DeviceID_1);
		
		map(self:leds[GREEN], system:leds[GREEN]) param (DeviceID_1, SensorID_1);
		map(self:leds[BLUE], system:leds[BLUE]) param (DeviceID_1, SensorID_2);
		map(self:relays[A], system:relays[A]) param (DeviceID_1, SensorID_5);
		map(self:relays[B], system:relays[B]) param (DeviceID_1, SensorID_6);
		
		var Evaluation eval := Evaluation.create alive;
		map(eval:motor, system:motor) param (DeviceID_1, SensorID_3);
		map(eval:rgbSensor, system:rgbSensor) param (DeviceID_1, SensorID_4, RGB_REFRESH_RATE);
		map(eval:echoSensor, system:echoSensor) param (DeviceID_1, SensorID_8, LED_REFRESH_RATE);
		
		var default cfgUpdates := activate(configUpdates());

		switchLamp(false);
		t.start(1.0); t.timeout;
		leds[GREEN].send(switchLED_OFF);
		t.start(1.0); t.timeout;
		leds[BLUE].send(switchLED_ON);
		
		eval.start(f_SetupTest1());
		eval.done;
		
		if (getverdict() == pass) {
			switchLamp(true);
			t.start(TIMEOUT); t.timeout;
			switchLamp(false);
		} else {
			switchLamp(true);
			t.start(1.0); t.timeout;
			switchLamp(false);
			t.start(1.0); t.timeout;
			switchLamp(true);
			t.start(1.0); t.timeout;
			switchLamp(false);
		}

		t.start(1.0); t.timeout;
		leds[GREEN].send(switchLED_ON);
		t.start(1.0); t.timeout;
		leds[BLUE].send(switchLED_OFF);
		setverdict(pass);
	}
	testcase SetupTestLEDs() runs on me system PhyIO {
		timer t;
		map(self:config, system:config) param (px_rs232Configuration, DeviceID_1);
		
		map(self:leds[GREEN], system:leds[GREEN]) param (DeviceID_1, SensorID_1);
		map(self:leds[BLUE], system:leds[BLUE]) param (DeviceID_1, SensorID_2);

		leds[GREEN].send(switchLED_ON);
		
		t.start(1.0); t.timeout;

		leds[BLUE].send(switchLED_OFF);
		
		t.start(1.0); t.timeout;

		leds[GREEN].send(switchLED_OFF);
		
		t.start(1.0); t.timeout;

		leds[BLUE].send(switchLED_ON);

		t.start(1.0); t.timeout;

		leds[GREEN].send(switchLED_ON);
		
		t.start(1.0); t.timeout;

		leds[BLUE].send(switchLED_ON);

		t.start(2.0); t.timeout; // wait for arduino output
		
		leds[BLUE].send(switchLED_OFF);
		
		t.start(1.0); t.timeout;

		leds[GREEN].send(switchLED_OFF);
		
		t.start(1.0); t.timeout;
		
		setverdict(pass);
	}
	
	testcase SetupTestRGB() runs on me system PhyIO {
		var RGB rgb;
		timer t;

		map(self:config, system:config) param (px_rs232Configuration, DeviceID_1);
		
		map(self:leds[BLUE], system:leds[BLUE]) param (DeviceID_1, SensorID_2);
		map(self:rgbSensor, system:rgbSensor) param (DeviceID_1, SensorID_4, RGB_REFRESH_RATE);
		
		var default cfgUpdates := activate(configUpdates());

		t.start(1.0); t.timeout;

		leds[BLUE].send(switchLED_ON);
		
		t.start(3.0); t.timeout;

		rgbSensor.send(ReadRGB:{});
		t.start(TIMEOUT);
		alt {
			[]rgbSensor.receive(RGB:{?, ?, ?, ?, ?, ?, ?, ?, ?, ?}) -> value rgb {
				// TODO test color
				log("Color detected ", rgb);
				setverdict(pass);
			}
			[]t.timeout {
				setverdict(inconc);
			}
		}
		
		t.start(2.0); t.timeout;

		leds[BLUE].send(switchLED_ON);

		t.start(1.0); t.timeout;

		leds[BLUE].send(switchLED_OFF);
	}

	function switchLamp(boolean switchOn) runs on me {
		relays[A].send(RelaySwith:switchOn);
		timer t := 2.0;
		t.start; t.timeout;
		relays[B].send(RelaySwith:not(switchOn));
		t.start; t.timeout;
	}
	
	function f_ReadDistance(float p_waitTime := 10.0) runs on Evaluation system PhyIO return Distance {
		var Distance distance := -1.0;
		timer t := p_waitTime;
		log("Reading distance");
		echoSensor.send(ReadDistance:{});
		echoSensor.send(ReadDistance:{});
		//	PING < 30 cm start test
		t.start;
		alt {
			[]echoSensor.receive(Distance:?) -> value distance {
				log(sprintf1<float>("Received distance %f cm", distance));
			}
			[]t.timeout {
				log("Distance sensor not responding");
			}
		}
		return distance;
	}

	function f_SetupTest1() runs on Evaluation system PhyIO {
		var RGB rgb;
		timer t;
		timer wait20seconds := 20.0;
		
		var Distance distance := 200.0;
		wait20seconds.start;
		while (distance > 30.0 and distance < 0.1) {
			distance := f_ReadDistance(3.0);
			if (not(wait20seconds.running)) {
				setverdict(inconc, "Distance sensor not triggered.");
				break;
			}
		}
		
		
		t.start(2.0); t.timeout;
		motor.send(Velocity:50);
		t.start(5.0); t.timeout;
		motor.send(Velocity:0);
		t.start(8.0); t.timeout;
		motor.send(Velocity:-50);
		t.start(5.0); t.timeout;
		motor.send(Velocity:0);
		t.start(10.0); t.timeout;
		
		rgbSensor.send(ReadRGB:{});
		t.start(TIMEOUT);
		alt {
			[]rgbSensor.receive(RGB:{?, ?, ?, ?, ?, ?, ?, ?, ?, ?}) -> value rgb {
				log("Color detected ", rgb);
				setverdict(pass);
			}
			[]t.timeout {
				setverdict(inconc);
			}
		}
	}
	
	altstep configUpdates() runs on Configuration {
		[]config.receive {
			repeat;
		}
	}

} with {
	encode "PhyIO"
}