module Functions {
	import from PhyIOAUX all;
	import from Components all;
	
	/**
	 * @desc Test to check for valid rfid
	 * @param RFID in question, time until timeout
	 */
	function f_checkRFID(RFID rfid, float time) runs on me system SUT return verdicttype {
			
		rfidSensor.send(ReadRFID:{});
		
		t.start(time);
		
		alt{
			[]rfidSensor.receive(RFID: rfid){
				return pass;
			}
			[]rfidSensor.receive(RFID: ?) {
				return fail;
			}
			[]t.timeout {
				return inconc;
			}
		}
		
		return inconc;
	}
	/** @desc checks if EmergencyLights are working correctly
	 *  @param 	switchTimetarget: time to next status change
	 *  		allowedSwitchTimeDeviation: allowed delay between status changes
	 *  		time: time until timeout
	 */
	function f_checkEmergencyLights(integer switchTimeTarget, integer allowedSwitchTimeDeviation, integer minSwitches, float time)
	runs on me system SUT return verdicttype{
		var verdicttype result := none;
		
		var State state;
		var Timestamp lastTimestamp := null;
		var boolean isTimeout;
		var integer switchCount := 0;
		
		lightSensor.send(ReadLightSwitchStart:{});
		
		t.start(time);

		while(result == none){
			alt{
				[]lightSensor.receive(State: {?, ?}) -> value state {
					switchCount := switchCount + 1;
					
					if(lastTimestamp == null){
						lastTimestamp := state.time;
					} else {
						if(f_abs(state.time - lastTimestamp - switchTimeTarget) > allowedSwitchTimeDeviation){
							result := fail;
						} else {
							lastTimestamp := state.time;
						}
					}
				}
				[]t.timeout {
					isTimeout := true;
					result := inconc;
				}
			}
		}
		
		if(result != none){
			lightSensor.send(ReadLightSwitchStop:{});
			return result;
		}
		
		lightSensor.receive(State: {?, ?}) -> value state;
		lightSensor.send(ReadLightSwitchStop:{});
			
		if(state.time - lastTimestamp <= switchTimeTarget + allowedSwitchTimeDeviation
			and switchCount >= minSwitches){
			return pass;
		}
		else{
			return fail;
		}
		
	}
	
	function f_abs(integer a) return integer{
		if(a < 0){return -a;}
		else{return a;}
	}
}