/**
 * @shortdesc Theft Detection
 * @desc Testcase that tests the theft detection
 */
module TheftDetection {
	import from PhyIOAUX all;
	import from TTlibrary_UnitTest all;
	
	import from Components all;
	import from Functions all;
	import from Parameters all;
	
	/**
	 * @shortdesc Theft Detection (correct RFID, door opens)
	 * @desc Theft detection with the correct RFID and door opening. Expect no alarm.
	 * @verdict pass if RFID is correct, door opens and no alarm. inconc if light sensor doesn't inform about
	 * 			the alarm. Otherwise fail
	 * @stage 1:Read correct RFID
	 * @stage 2:Wait for door
	 * @stage 3:Check for alarm
	 */
	testcase TheftDetectionTestcase_RfidOk_DoorOpens() runs on me system System {
		var VerdictDescription verdict := internalTheftDetection(pass, true, false);
		setverdict(verdict.verdict, verdict.reason);
	}
	
	/**
	 * @shortdesc Theft Detection (correct RFID, door remains closed)
	 * @desc Theft detection with the correct RFID and door doesn't open. Expect no alarm.
	 * @verdict pass if RFID is correct, door doesn't open and no alarm. inconc if light sensor doesn't inform about
	 * 			the alarm. Otherwise fail
	 * @stage 1:Read correct RFID
	 * @stage 3:Check for alarm
	 */
	testcase TheftDetectionTestcase_RfidOk_DoorClosed() runs on me system System {
		var VerdictDescription verdict := internalTheftDetection(pass, false, false);
		setverdict(verdict.verdict, verdict.reason);
	}
	
	/**
	 * @shortdesc Theft Detection (false RFID, door opens)
	 * @desc Theft detection with a false RFID and door opening. Expect alarm.
	 * @verdict pass if RFID is false, door opens and alarm goes off. inconc if light sensor doesn't inform about
	 * 			the alarm. Otherwise fail
	 * @stage 1:Read false RFID
	 * @stage 2:Wait for door
	 * @stage 3:Check for alarm
	 */
	testcase TheftDetectionTestcase_RfidFalse_DoorOpens() runs on me system System {
		var VerdictDescription verdict := internalTheftDetection(fail, true, true);
		setverdict(verdict.verdict, verdict.reason);
	}
	
	/**
	 * @shortdesc Theft Detection (flase RFID, door remains closed)
	 * @desc Theft detection with the false RFID and door doesn't open. Expect no alarm.
	 * @verdict pass if RFID is false, door doesn't open and no alarm. inconc if light sensor doesn't inform about
	 * 			the alarm. Otherwise fail
	 * @stage 2:Read false RFID
	 * @stage 3:Check for alarm
	 */
	testcase TheftDetectionTestcase_RfidFalse_DoorClosed() runs on me system System {
		var VerdictDescription verdict := internalTheftDetection(fail, false, false);
		setverdict(verdict.verdict, verdict.reason);
	}
		
	/**
	 * @shortdesc Theft Detection (no RFID, door opens)
	 * @desc Theft detection with no RFID and door opening. Expect alarm.
	 * @verdict pass if RFID is missing, door opens and alarm goes off. inconc if light sensor doesn't inform about
	 * 			the alarm. Otherwise fail
	 * @stage 2:Wait for door
	 * @stage 3:Check for alarm
	 */
	testcase TheftDetectionTestcase_RfidMissing_DoorOpens() runs on me system System {
		var VerdictDescription verdict := internalTheftDetection(inconc, true, true);
		setverdict(verdict.verdict, verdict.reason);
	}
	
	/**
	 * @shortdesc Theft Detection (no RFID, door remains closed)
	 * @desc Theft detection with the no RFID and door doesn't open. Expect no alarm.
	 * @verdict pass if RFID is missing, door doesn't open and no alarm. inconc if light sensor doesn't inform about
	 * 			the alarm. Otherwise fail
	 * @stage 3:Check for alarm
	 */
	testcase TheftDetectionTestcase_RfidMissing_DoorClosed() runs on me system System {
		var VerdictDescription verdict := internalTheftDetection(inconc, false, false);
		setverdict(verdict.verdict, verdict.reason);
	}
	
	function internalTheftDetection(verdicttype expectRFID, boolean expectDoor, boolean expectEmergency)
	runs on me system System return VerdictDescription {
		var VerdictDescription result := {none};
		timer requestELTimer;
		
		const float EL_CHECK_TIME := 0.5;
		const float WAIT_FOR_EL_TIME := 3.0;
		
		var boolean hasDoor := false;
		var boolean hasRFID := false;
		var boolean isRFIDCorrect := false;
		var boolean isAlarmOn := false;
		
		setup();
		
		doorSensor.send(ReadDoorStart: {});
		lightSensor.send(ReadLightSwitchStart: MIN_SWITCH_BRIGHTNESS_DIFFERENCE);
		rfidSensor.send(RFID: OWNER_RFID);
		rfidSensor.send(ReadRFIDStart: {});
		
		t.start(TIME_THEFT_DETECTION);
		requestELTimer.start(EL_CHECK_TIME);
		
		while(result.verdict == none) {
			alt {
				[not hasDoor]doorSensor.receive(DoorState: true) {
					log("Door opened.");
					setStage(3, WAIT_FOR_EL_TIME);
					
					hasDoor := true;

					t.start(WAIT_FOR_EL_TIME);
				}
				[]doorSensor.receive(DoorState: ?) {
				// there is nothing to see here, please continue
				}
				[not isAlarmOn]requestELTimer.timeout {
					lightSensor.send(READ_FREQUENCY);
					requestELTimer.start(EL_CHECK_TIME);
				}
				[]lightSensor.receive(Frequency: 0.0) {
				// there is nothing to see here, please continue
				}
				[]lightSensor.receive(Frequency: CORRECT_FREQUENCY) {
					log("Emergency light on, frequency correct.");
					isAlarmOn := true;
					break;
				}
				[]lightSensor.receive(Frequency: ?) {
					// false emergency lights frequency
					result := verdictFail("Emergency lights blink in incorrect frequency");
				}
				[]rfidSensor.receive(RFID: OWNER_RFID) {
					if(not hasRFID or not isRFIDCorrect) {
						log("Received correct RFID.");
					}
					
					if(not hasRFID) {
						// restart timer here because we want to give extra time to push button
						if(expectDoor) {
							setStage(2, TIME_THEFT_DETECTION);
							t.start(TIME_THEFT_DETECTION);
						} else {
							setStage(3, WAIT_FOR_EL_TIME);
							t.start(WAIT_FOR_EL_TIME);
						}
					}
					
					hasRFID := true;
					isRFIDCorrect := true;
				}
				[]rfidSensor.receive(RFID: ?) {
					// false RFID
					if(not hasRFID or isRFIDCorrect) {
						log("Received false RFID.");
					}
					
					if(not hasRFID) {
						// restart timer here because we want to give extra time to push button
						if(expectDoor) {
							setStage(2, TIME_THEFT_DETECTION);
							t.start(TIME_THEFT_DETECTION);
						} else {
							setStage(3, WAIT_FOR_EL_TIME);
							t.start(WAIT_FOR_EL_TIME);
						}
					}
					
					hasRFID := true;
					isRFIDCorrect := false;
				}
				[]t.timeout {
					log("Timeout.");
					break;
				}
			}
		}
		
		doorSensor.send(ReadDoorStop: {});
		lightSensor.send(ReadLightSwitchStop: {});
		rfidSensor.send(ReadRFIDStop: {});
		
		tearDown();
		
		if(not expectEmergency and isAlarmOn) {
			return verdictFail("Alarm shouldn't have gone off");
		} else if(expectEmergency and not isAlarmOn) {
			return verdictFail("Alarm should have gone off");
		} else if(not expectDoor and hasDoor) {
			return verdictFail("Door shouldn't have opened");
		} else if(expectDoor and not hasDoor) {
			return verdictFail("Door should have opened");
		}

		if(expectRFID == inconc) {
			if(hasRFID) {
				return verdictFail("Expected no RFID");
			} else {
				return verdictPass("");
			}
		} else {
			if(isRFIDCorrect and expectRFID == fail) {
				return verdictFail("Expected false RFID");
			} else if(not isRFIDCorrect and expectRFID == pass) {
				return verdictFail("Expected correct RFID");
			} else {
				return verdictPass("");
			}
		}
	}
	
	/**
	 * @deprecated doesn't use latest communication standard with light sensor, also parameters are kinda meh...
	 */
	testcase OldTheftDetectionTestcase(verdicttype expectRFID, boolean expectDoor, verdicttype expectEmergency) runs on me system System{
		var verdicttype v_rfid := none;
		var verdicttype v_elights := none;
		var boolean doorOpened := false;
		
		var me rfidComponent := me.create;
		var me elightsComponent := me.create;
		
		setup();
		
		connect(self:rfidPort, rfidComponent:rfidPort);
		connect(self:emergencyLightPort, elightsComponent:emergencyLightPort);
		
		rfidComponent.start(executeRFIDDetection(OWNER_RFID, THEFT_RFID_CHECK_RATE));
		elightsComponent.start(executeEmergencyLightDetection(SWITCH_TIME_TARGET, ALLOWED_SWITCH_TIME_DEVIATION, MIN_SWITCH_BRIGHTNESS_DIFFERENCE, 5.0));
		
		doorSensor.send(ReadDoorStart:{});
		
		t.start(30.0);
		
		while(doorOpened == false and getverdict == none){
			alt {
				[]rfidPort.receive(verdicttype: ?) -> value v_rfid {
				}
				[]emergencyLightPort.receive(verdicttype: ?) -> value v_elights {
				}
				[]doorSensor.receive(DoorState: true){
					doorOpened := true;
				}
				[]t.timeout {
					setverdict(inconc);
				}
			}
		}
		
		doorSensor.send(ReadDoorStop:{});
		if(v_rfid != pass) {
			rfidPort.send(StopThread: {});
		}
		
		if(v_elights != pass){
		
			// wait for emergency
			t.start(10.0);
		
			alt {
				[]emergencyLightPort.receive(verdicttype: ?) -> value v_elights {
				}
				[]t.timeout {
					setverdict(inconc);
				}
			}
		
			emergencyLightPort.send(StopThread: {});
		}
		
		if(getverdict != none) {
		// we're done
		} else if(	expectDoor != doorOpened or
			v_rfid != expectRFID or
			v_elights != expectEmergency and not (v_elights == inconc and expectEmergency == fail)) {
			setverdict(fail);
		} else {
			setverdict(pass);
		}
		
		rfidComponent.done;
		elightsComponent.done;
		
		disconnect(self:rfidPort, rfidComponent:rfidPort);
		disconnect(self:emergencyLightPort, elightsComponent:emergencyLightPort);
		
		tearDown();
	}
	
	function executeRFIDDetection(RFID rfid, float checkRate) runs on me system System {
		
		var verdicttype result;
		timer loopTimer;
		
		rfidSensor.send(OWNER_RFID);
		
		while(true) {
			loopTimer.start(checkRate);
			
			result := checkRFID(rfid, checkRate);
			
			rfidPort.send(result);
			
			if(result == pass) {
				stop;
			}
			
			alt {
				[]rfidPort.receive(StopThread: {}) {
					stop;
				}
				[]loopTimer.timeout {
				// do nothing
				}
			}
		}
	}
	/**
		 * @deprecated doesn't use latest communication standard with light sensor, also parameters are kinda meh...
		 */
	function executeEmergencyLightDetection(float switchTime, float allowedDeviation, integer minDiff, float checkRate) runs on me system System {
		
		var verdicttype result;
		timer loopTimer;
		
		while(true) {
			loopTimer.start(checkRate);
			
			result := checkEmergencyLights(switchTime, allowedDeviation, minDiff, float2int(checkRate / switchTime / 2.0), checkRate);
			
			emergencyLightPort.send(result);
			
			if(result == pass) {
				stop;
			}
			
			alt {
				[]emergencyLightPort.receive(StopThread: {}) {
					stop;
				}
				[]loopTimer.timeout {
				// do nothing
				}
			}
		}
	}
	
	function executeTheftDetectionTestcases() {
		execute(TheftDetectionTestcase_RfidOk_DoorOpens());
		execute(TheftDetectionTestcase_RfidOk_DoorClosed());
		execute(TheftDetectionTestcase_RfidFalse_DoorOpens());
		execute(TheftDetectionTestcase_RfidFalse_DoorClosed());
		execute(TheftDetectionTestcase_RfidMissing_DoorOpens());
		execute(TheftDetectionTestcase_RfidMissing_DoorClosed());
	}
}
